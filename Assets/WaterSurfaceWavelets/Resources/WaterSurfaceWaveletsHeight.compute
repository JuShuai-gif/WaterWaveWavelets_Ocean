#pragma kernel HeightMainCS

static const float tau = 6.28318530718f;
static const int count = 16;
static const int numNodes = count * 8;
static const int seed = 40234324;

uint2 size;
float period;

Texture3D<float> ampl;
Texture2D<float4> profile;
RWTexture2D<float3> result;

SamplerState samplerInputLinearClamp;
SamplerState samplerInputLinearRepeat;

float amplitude(float3 p)
{
	return ampl.SampleLevel(samplerInputLinearClamp, p, 0.0f);
}

float3 waveHeight(float2 p)
{
	float3 result = float3(0.0f, 0.0f, 0.0f);

	float da = 1.0f / numNodes;
	float dx = 16.0f * tau / numNodes;
	for (float a = 0; a < 1.0f; a += da)
	{
		float angle = a * tau;
		float2 kdir;
		sincos(angle, kdir.y, kdir.x);
		float kdir_x = dot(p.xy, kdir) + tau * sin(seed * a);
		float w = 10.0f * kdir_x / period;

		float4 tt = dx * amplitude(float3(p, a)) * profile.SampleLevel(samplerInputLinearRepeat, w, 0.0f);

		result.xz += kdir * tt.x;
		result.y += tt.y;
	}

	return result;
}

[numthreads(1, 1, 1)]
void HeightMainCS(uint2 DTid : SV_DispatchThreadID)
{
	float2 s = size;
	float2 p = DTid / s;
	result[DTid] = waveHeight(p);
}
